plugins {
    alias libs.plugins.com.android.application
    alias libs.plugins.org.jetbrains.kotlin.android
}

apply from: "$rootProject.projectDir/gradle/android-common.gradle"

def getValueFromPropertiesFile = { propFile, key ->
    if (!propFile.isFile() || !propFile.canRead())
        return null
    def prop = new Properties()
    def reader = propFile.newReader()
    try {
        prop.load(reader)
    } finally {
        reader.close()
    }
    return prop.get(key)
}

def getProperty = { name, defValue ->
    def prop = project.properties[name] ?:
            getValueFromPropertiesFile(project.rootProject.file('local.properties'), name)
    return (null == prop) ? defValue : prop
}

ext.dittoOnlinePlaygroundAppId = getProperty('ditto.onlinePlayground.appId', "null")
ext.dittoOnlinePlaygroundToken = getProperty('ditto.onlinePlayground.token', "null")

android {
    namespace "live.ditto.dittotoolsapp"

    defaultConfig {
        applicationId "live.ditto.dittotoolsapp"
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "DITTO_ONLINE_PLAYGROUND_APP_ID", dittoOnlinePlaygroundAppId
        buildConfigField "String", "DITTO_ONLINE_PLAYGROUND_TOKEN", dittoOnlinePlaygroundToken
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    androidTestImplementation libs.androidx.test.ext.junit

    implementation libs.androidx.compose.material3.material3
    implementation libs.androidx.compose.ui.ui
    implementation libs.androidx.compose.ui.uiToolingPreview
    implementation libs.androidx.navigation.navigationCompose
    implementation libs.live.ditto.ditto

    implementation platform(libs.androidx.compose.composeBom)

    implementation libs.live.ditto.databrowser
    implementation libs.live.ditto.exportlogs
    implementation libs.live.ditto.presenceviewer
    implementation libs.live.ditto.heartbeat

    // TODO: Use maven version once this ticket is done
    //       https://github.com/getditto/DittoAndroidTools/issues/31
    implementation(project(":DittoDiskUsage"))
    // implementation libs.live.ditto.diskusage

    implementation libs.live.ditto.health
    implementation(project(":DittoPresenceDegradationReporter"))

    testImplementation libs.junit.junit
}