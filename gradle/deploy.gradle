apply plugin: 'maven-publish'
apply plugin: 'signing'



def signingKey = findProperty('SIGNING_KEY')
def signingKeyPwd = findProperty('SIGNING_KEY_PWD')

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
    sign publishing.publications
}

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId libraryArtifactId

                pom {
                    name = 'Ditto Tools'
                    description = 'Ditto Tools'
                    url = 'https://ditto.live'

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/license/mit/'
                        }
                    }

                    // https://github.com/getditto/DittoAndroidTools
                    scm {
                        connection = 'scm:git:github.com/getditto/DittoAndroidTools.git'
                        developerConnection = 'scm:git:ssh://github.com/getditto/DittoAndroidTools.git'
                        url = 'https://ditto.live'
                    }

                    developers {
                        developer {
                            id = 'ditto'
                            name = 'Ditto'
                            email = 'support@ditto.live'
                        }
                    }
                }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}