apply plugin: 'maven-publish'
apply plugin: 'signing'

def signingKeyBase64 = findProperty("GPG_SIGNING_IN_MEMORY_KEY")
def signingPassword = findProperty("GPG_SIGNING_IN_MEMORY_KEY_PWD")

static def base64Decode(encodedString){
    if(encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    }
    return null
}

signing {
    if(project.hasProperty("PUBLISH_TO_MAVEN_CENTRAL")){
        useInMemoryPgpKeys(base64Decode(signingKeyBase64), signingPassword)
        sign publishing.publications
    }
}

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId libraryArtifactId
                groupId = 'live.ditto'

                version = findProperty("LIBRARY_VERSION") ?: "SNAPSHOT"

                pom {
                    name = 'Ditto Tools'
                    description = 'Ditto Tools'
                    url = 'https://ditto.live'

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/license/mit/'
                        }
                    }

                    // https://github.com/getditto/DittoAndroidTools
                    scm {
                        connection = 'scm:git:github.com/getditto/DittoAndroidTools.git'
                        developerConnection = 'scm:git:ssh://github.com/getditto/DittoAndroidTools.git'
                        url = 'https://ditto.live'
                    }

                    developers {
                        developer {
                            id = 'ditto'
                            name = 'Ditto'
                            email = 'support@ditto.live'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username = findProperty("MAVEN_CENTRAL_USERNAME")
                    password = findProperty("MAVEN_CENTRAL_PASSWORD")
                }

                def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")

                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    jreleaser {
        gitRootSearch = true

        project {
            name = 'Ditto Tools'
            description = 'Ditto Tools for Android'
            website = 'https://ditto.com'
            authors = ['Ditto']
            license = 'MIT'
            version = findProperty("LIBRARY_VERSION") ?: "1.0.0-SNAPSHOT"
        }

        release {
            github {
                skipRelease = false  // Automatically create GitHub releases when publishing
                token = System.getenv('JRELEASER_GITHUB_TOKEN') ?: 'PLACEHOLDER_FOR_LOCAL_TESTING'
            }
        }

        signing {
            // Enable signing only when GPG keys are available (e.g., in CI)
            active = System.getenv('JRELEASER_GPG_SECRET_KEY') ? 'ALWAYS' : 'NEVER'
            armored = true
            // Environment variables:
            // - JRELEASER_GPG_PUBLIC_KEY (optional)
            // - JRELEASER_GPG_SECRET_KEY
            // - JRELEASER_GPG_PASSPHRASE
        }

        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/staging-deploy')
                        // Credentials via environment variables:
                        // - JRELEASER_MAVENCENTRAL_USERNAME
                        // - JRELEASER_MAVENCENTRAL_PASSWORD
                    }
                }
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}